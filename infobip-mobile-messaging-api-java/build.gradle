buildscript {
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'maven'
    id 'idea'
}

apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

shadowJar {
    classifier = 'shaded'
    zip64 = true
    relocate('com.google.gson', 'org.infobip.mobile.messaging.api.shaded.google.gson')
    relocate('org.apache.commons.codec', 'org.infobip.mobile.messaging.api.shaded.apache.commons.codec')
    manifest {
        attributes("Implementation-Title": "Gradle", "Implementation-Version": version)
    }
}

jar.dependsOn(shadowJar)

jar {
    from{
        zipTree(shadowJar.archivePath)
    }{
        include 'org/infobip/mobile/messaging/api/shaded/**'
    }
}

configurations {
    provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
    test { compileClasspath += configurations.provided }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

idea {
    module {
        try {
            scopes.PROVIDED.plus += [project.configurations.provided]

            //scopes.PROVIDED.plus += project.configurations.provided.compile
            //scopes.PROVIDED.plus += project.configurations.provided.runtime
            //scopes.COMPILE.minus += project.configurations.provided.compile
            //scopes.RUNTIME.minus += project.configurations.provided.runtime
        } catch (NullPointerException e) {
            println ":${project.name} ${e}"
        }
    }
}

dependencies {
    compile "com.google.code.gson:gson:2.8.2"
    compile "commons-codec:commons-codec:1.10"

    provided "org.projectlombok:lombok:1.16.10"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:2.3.0"
    testCompile "com.nanohttpd:nanohttpd:2.1.0"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile "org.mockito:mockito-core:1.10.19"
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'infobip-mobile-messaging-api-java'
        userOrg = 'infobip'
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/infobip/mobile-messaging-sdk-android"
    }
}
