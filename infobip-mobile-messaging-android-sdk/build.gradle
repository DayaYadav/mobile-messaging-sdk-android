buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath "org.projectlombok:lombok:1.16.8"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName project.version
        testInstrumentationRunner 'org.infobip.mobile.messaging.service.CustomInstrumentationTestRunner'
//        setProperty("archivesBaseName", "${archivesBaseName}-$versionName")
    }

    lintOptions {
        abortOnError false
    }

    compileOptions{
        sourceCompatibility=JavaVersion.VERSION_1_7
        targetCompatibility=JavaVersion.VERSION_1_7
    }
}

task copyLibs(type: Copy) {
    from(configurations.compile){
        include "infobip-mobile-messaging-api-java*"
    }
    into 'libs'
}

assemble.dependsOn(copyLibs)

configurations {
    provided
    compile.extendsFrom provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
    androidTest { compileClasspath += configurations.provided }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile(project(':infobip-mobile-messaging-api-java'))

    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'

    provided "org.projectlombok:lombok:1.16.8"

    compile group: 'com.google.android.gms', name: 'play-services', version: '8.4.0'
}

idea {
    module {
        try {
            scopes.PROVIDED.plus += [project.configurations.provided]

            //scopes.PROVIDED.plus += project.configurations.provided.compile
            //scopes.PROVIDED.plus += project.configurations.provided.runtime
            //scopes.COMPILE.minus += project.configurations.provided.compile
            //scopes.RUNTIME.minus += project.configurations.provided.runtime
        } catch(NullPointerException e) {
            println ":${project.name} ${e}"
        }
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def jar = project.tasks.create(name: "jar${name.capitalize()}", type: Jar) {
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir

        manifest {
            attributes(
                    "Bundle-Name": 'parse-android',
                    "Bundle-Version": project.version
            )
        }

        exclude '**/R.class'
        exclude '**/R\$*.class'
        exclude '**/Manifest.class'
        exclude '**/Manifest\$*.class'
        exclude '**/BuildConfig.class'
    }

    artifacts {
        archives jar
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.name
        vcsUrl = "http://git.infobip.local/projects/MML/repos/infobip-mobile-messaging-android/browse"
        licenses = ['Apache-2.0']
        labels = ['aar', 'android']
    }
}
